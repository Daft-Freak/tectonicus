plugins {
	id 'org.ajoberstar.grgit' version '1.6.0'
}

apply plugin: 'java'

archivesBaseName = 'Tectonicus'
version = '2.24'
sourceCompatibility = 1.7
targetCompatibility = 1.7

sourceSets.main.java.srcDirs = ['Source']

compileJava {
	options.debug = false
}

repositories {
	mavenCentral()
}

dependencies {
	compile('org.lwjgl.lwjgl:lwjgl_util:2.9.3@jar',
			'org.apache.commons:commons-lang3:3.5@jar',
			'com.google.code.gson:gson:2.8.0@jar') 
	compile('org.lwjgl.lwjgl:lwjgl:2.9.3') {
				exclude module: 'jinput'
			}
	compile files ('Libs/Jars/jnbt.jar', 'Libs/Jars/jpct.jar', 'Libs/Jars/ProcessingCore.jar', 'Libs/Jars/BiomeExtractor.jar')
	
	testCompile 'junit:junit:4.12'
}

build.mustRunAfter clean

build.doFirst {
	def props = new Properties()
	props.setProperty('buildDate', new Date().format('MMMM dd yyyy'))
	props.setProperty('buildTime', new Date().format('hhmm'))
	props.setProperty('buildNumber', grgit.log().size().toString())
	props.setProperty('version', "${version}")
	def writer = new FileWriter(file('build/tectonicus.buildInfo'))
	props.store(writer, 'Tectonicus build info')
	writer.flush()
	writer.close()
}

jar {
	from files('build/tectonicus.buildinfo')
	from configurations.runtime.asFileTree.files.collect { zipTree(it) }
	from fileTree('Data')
	exclude ('OpenAL*.dll', 'openal.dylib', 'libopenal*.so', 'processing/**')

	manifest {
		attributes 'Main-Class': 'tectonicus.TectonicusApp'
	}
}

task createPackage(type: Zip, dependsOn: ['clean', 'build'], group: 'build', description: 'Create release zip file') {
	from files('Data/defaultBlockConfig.xml', "build/libs/tectonicus-${version}.jar")
	from('Docs') {
		include('*.xml', '*.txt')
	}
}
